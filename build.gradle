buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "de.pajowu.vp"
            minSdkVersion 10
            targetSdkVersion 23
            versionCode code
            versionName "2.0.3"
            resConfigs "de"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_FILE"))
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "vpapp"
            keyPassword System.getenv("STORE_PASSWORD")
        }
    }

    buildTypes {
        all { 
            buildConfigField "String", "GCM_SENDER", "\"1085192535095\""
            buildConfigField "String", "ADMOB_APP_ID", "\"ca-app-pub-3222558331255580~3727158351\""
            resValue "string", "banner_ad_unit_id", "ca-app-pub-3940256099942544/6300978111"
        }
        debug {
            buildConfigField "String", "BASE_URL", "\"http://10.0.0.7:8080/_ah/api/vp/v1/\""
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "VP DEBUG"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://release-dot-vpapp-bmmsz.appspot.com/_ah/api/vp/v1/\""
            resValue "string", "app_name", "Vertretungsplan"
            signingConfig signingConfigs.release
        }
    }

    
    
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.+'
    compile 'com.android.support:recyclerview-v7:23.+'

    compile 'com.google.firebase:firebase-ads:9.0.0'
    compile 'com.google.firebase:firebase-messaging:9.0.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    compile 'com.google.code.gson:gson:2.2.4'


}

apply plugin: 'com.google.gms.google-services'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
